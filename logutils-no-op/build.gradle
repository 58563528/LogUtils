apply plugin: 'com.android.library'

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode 32
        versionName "1.5.1.1"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
}

if (file("$projectDir/bintrayUpload.gradle").exists()) {
    apply from: "./bintrayUpload.gradle"
}

task sync << {
    def javaPath = "src/main/java/com/apkfuns/logutils"
    copy {
        from "$rootDir/library/${javaPath}"
        include "LogConfig.java","Parser.java", "Printer.java"
        into "$projectDir/${javaPath}"
    }
    File parserFile = project.file("${javaPath}/Parser.java")
    def parserContent = parserFile.getText().replaceFirst(/String LINE_SEPARATOR = .+;/, "String LINE_SEPARATOR = null;")
    parserFile.write(parserContent)
}

